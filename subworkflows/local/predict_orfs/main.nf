// predict ORFs with transdecoder & output fasta for msfragger
include { PHILOSOPHER_DATABASE } from '../../../modules/local/philosopher/database/main'
include { DIAMOND_MAKEDB       } from '../../../modules/nf-core/diamond/makedb/main'
include { DIAMOND_BLASTP       } from '../../../modules/nf-core/diamond/blastp/main'
include { TRANSDECODER_LONGORF } from '../../../modules/local/transdecoder/longorf/main'
include { TRANSDECODER_PREDICT } from '../../../modules/local/transdecoder/predict/main'
include { TRANSDECODER2FASTA   } from '../../../modules/local/transdecoder2fasta/main'
workflow PREDICT_ORFS {
    take:
    fasta_ch // channel: [ val(meta), fasta, fusion_table ]
    blast_db // path to diamond blast database if it already exists

    main:

    ch_versions = Channel.empty()
    // prepare diamond database for diamond blast
    if (!blast_db) {
        PHILOSOPHER_DATABASE([id: params.UPID], params.reviewed, params.isoforms)
        blast_fasta = PHILOSOPHER_DATABASE.out.fasta
        ch_versions = ch_versions.mix(PHILOSOPHER_DATABASE.out.versions)
    }
    else {
        blast_fasta = [[id: params.UPID], blast_db]
    }
    // make diamond database
    DIAMOND_MAKEDB(blast_fasta, [], [], [])
    ch_versions = ch_versions.mix(DIAMOND_MAKEDB.out.versions)
    // run transdecoder
    TRANSDECODER_LONGORF(fasta_ch)
    ch_versions = ch_versions.mix(TRANSDECODER_LONGORF.out.versions)
    // blast orfs against database
    DIAMOND_BLASTP(
        TRANSDECODER_LONGORF.out.pep,
        DIAMOND_MAKEDB.out.db,
        6,
        [],
    )
    ch_versions = ch_versions.mix(DIAMOND_BLASTP.out.versions)
    // join the cdna fasta with diamondblast and longorfs results
    input_predict_ch = fasta_ch
        .join(DIAMOND_BLASTP.out.txt)
        .join(TRANSDECODER_LONGORF.out.folder)
    // input_predict_ch.view()
    TRANSDECODER_PREDICT(input_predict_ch)
    ch_versions = ch_versions.mix(TRANSDECODER_PREDICT.out.versions)

    emit:
    ORFs      = TRANSDECODER_PREDICT.out.pep // channel: [ val(meta), fasta ]
    swissprot = blast_fasta
    versions  = ch_versions // channel: [ versions.yml ]
}
